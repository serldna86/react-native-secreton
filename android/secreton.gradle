ext.findWorkspaceRoot = { File startDir ->
    File current = startDir
    while (current != null) {
        if (new File(current, "package.json").exists()) {
            return current
        }
        current = current.parentFile
    }
    throw new RuntimeException("‚ùå package.json not found")
}

ext.loadEnvFile = { File file ->
    if (!file.exists()) return [:]

    def map = [:]
    file.eachLine { line ->
        line = line.trim()
        if (!line || line.startsWith("#")) return

        def idx = line.indexOf("=")
        if (idx <= 0) return

        def key = line.substring(0, idx)
        def value = line.substring(idx + 1)

        if (value.startsWith("\"") && value.endsWith("\"")) {
            value = value.substring(1, value.length() - 1)
        }

        map[key] = value
    }
    return map
}

ext.resolveEnvFromTasks = {
    def tasks = gradle.startParameter.taskNames.join(" ").toLowerCase()
    if (tasks.contains("prod")) return "prod"
    if (tasks.contains("uat")) return "uat"
    if (tasks.contains("preprod")) return "preprod"
    return "dev"
}

ext.secretonGenerateEnv = { appProject ->
    def androidDir = appProject.rootProject.rootDir
    def workspaceRoot = findWorkspaceRoot(androidDir)
    def rootDir = workspaceRoot

    def envName =
        appProject.findProperty("ENV")
        ?: System.getenv("ENV")
        ?: resolveEnvFromTasks()

    def envFileName =
        appProject.findProperty("ENVFILE")
        ?: System.getenv("ENVFILE")
        ?: ".env.${envName}"

    def envFile = new File(rootDir, envFileName)
    if (!envFile.exists()) {
        throw new RuntimeException("‚ùå ENV file not found: ${envFile.absolutePath}")
    }

    def possibleCliBins = [
        new File(workspaceRoot, "node_modules/.bin/rn-secreton-cli"),
        new File(workspaceRoot, "node_modules/.bin/rn-secreton-cli.cmd")
    ]

    def cliBin = possibleCliBins.find { it.exists() }
    if (!cliBin) {
        throw new RuntimeException("""‚ùå rn-secreton-cli not found.""")
    }

    def envMap = loadEnvFile(envFile)

    appProject.tasks.register("generateEnv", Exec) {

        group = "secreton"
        description = "Generate .env using rn-secreton-cli"

        workingDir rootDir

        envMap.each { k, v ->
            environment k, v
        }

        environment "ENV", envName
        environment "ENV_FILE", envFileName

        commandLine "node", cliBin.absolutePath, envName

        doFirst {
            println ""
            println "üîê rn-secreton"
            println "   ENV      : ${envName}"
            println "   ENV_FILE : ${envFileName}"
            println "   Loaded keys:"
            envMap.keySet().sort().each { println "    - $it" }
            println ""
        }
    }

    appProject.tasks.named("preBuild").configure {
        dependsOn "generateEnv"
    }
}
