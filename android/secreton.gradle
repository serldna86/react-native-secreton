/**
 * =========================
 * Secreton bootstrap
 * =========================
 */
ext.ensureLocalEnv = { appProject ->
    if (appProject.ext.has("localEnv")) return

    appProject.ext.localEnv = [:]
    appProject.ext.env = [:]

    def androidDir = appProject.rootProject.projectDir
    def workspaceRoot = findWorkspaceRoot(androidDir)
    def rootDir = workspaceRoot

    def envFileName =
        appProject.findProperty("ENVFILE")
        ?: System.getenv("ENVFILE")
        ?: ".env"

    def envFile = new File(rootDir, envFileName)
    if (!envFile.exists()) return

    appProject.ext.localEnv.putAll(loadEnvFile(envFile))
}

ext.findWorkspaceRoot = { File startDir ->
    File current = startDir
    while (current != null) {
        if (new File(current, "package.json").exists()) {
            return current
        }
        current = current.parentFile
    }
    throw new RuntimeException("âŒ package.json not found")
}

ext.loadEnvFile = { File file ->
    if (!file.exists()) return [:]

    def map = [:]
    file.eachLine { line ->
        line = line.trim()
        if (!line || line.startsWith("#")) return

        def idx = line.indexOf("=")
        if (idx <= 0) return

        def key = line.substring(0, idx)
        def value = line.substring(idx + 1)

        if (value.startsWith("\"") && value.endsWith("\"")) {
            value = value.substring(1, value.length() - 1)
        }

        map[key] = value
    }
    return map
}

ext.getSecretKey = { appProject ->
    ensureLocalEnv(appProject)
    return appProject.ext.localEnv.get("ENV_SECRET_KEY")
        ?: appProject.findProperty("ENV_SECRET_KEY")
        ?: System.getenv("ENV_SECRET_KEY")
}

ext.decryptValue = { String encrypted, String secretKey ->
    if (!encrypted) return encrypted
    if (!secretKey) {
        throw new RuntimeException("âŒ ENV_SECRET_KEY is required for decryption")
    }

    def cmd = """printf "%s" '${encrypted}' | openssl enc -aes-256-cbc -a -A -d -salt -pbkdf2 -iter 100000 -pass pass:${secretKey}"""

    def proc = ["bash", "-c", cmd].execute()
    proc.waitFor()

    if (proc.exitValue() != 0) {
        throw new RuntimeException(proc.err.text)
    }

    return proc.in.text.trim()
}

ext.loadAndDecryptEnv = { appProject ->
    ensureLocalEnv(appProject)

    def androidDir = appProject.rootProject.projectDir
    def rootDir = findWorkspaceRoot(androidDir)

    def envFileName =
        appProject.findProperty("ENVFILE")
        ?: System.getenv("ENVFILE")
        ?: ".env"

    def envFile = new File(rootDir, envFileName)
    if (!envFile.exists()) {
        throw new RuntimeException("âŒ ENV file not found: ${envFile.absolutePath}")
    }

    def secretKey = getSecretKey(appProject)
    def rawEnv = loadEnvFile(envFile)

    rawEnv.each { k, v ->
        def value = v
        if (v instanceof String && v.startsWith("enc:")) {
            value = decryptValue(v.substring(4), secretKey)
        }
        appProject.ext.env[k] = value
    }
}

ext.secretonGenerateEnv = { appProject ->
    def androidDir = appProject.rootProject.rootDir
    def workspaceRoot = findWorkspaceRoot(androidDir)
    def rootDir = workspaceRoot

    def envFileName =
        appProject.findProperty("ENVFILE")
        ?: System.getenv("ENVFILE")
        ?: ".env"

    def possibleCliBins = [
        new File(workspaceRoot, "../node_modules/.bin/rn-secreton-cli"),
        new File(workspaceRoot, "node_modules/.bin/rn-secreton-cli"),
        new File(workspaceRoot, "node_modules/.bin/rn-secreton-cli.cmd")
    ]

    def cliBin = possibleCliBins.find { it.exists() }
    if (!cliBin) {
        throw new RuntimeException("""âŒ rn-secreton-cli not found.""")
    }

    ensureLocalEnv(appProject)

    appProject.tasks.register("generateEnv", Exec) {

        group = "secreton"
        description = "Generate .env using rn-secreton-cli"

        workingDir rootDir

        doFirst {
            def envFile = new File(rootDir, envFileName)
            if (!envFile.exists()) {
                throw new RuntimeException("âŒ ENV file not found: ${envFile.absolutePath}")
            }

            def envMap = loadEnvFile(envFile)

            // Combine environment variables if react-native-config is used.
            if (!appProject.ext.has("env") || appProject.ext.env == null) {
                appProject.ext.env = [:]
            }
            if (!appProject.ext.has("react") || appProject.ext.env == null) {
                appProject.ext.react = [:]
            }

            envMap.each { k, v ->
                environment k, v
            }

            environment "ENV_FILE", envFileName
            
            println ""
            println "ğŸ” rn-secreton"
            println "   ENV_FILE : ${envFileName}"
            // println "   Loaded keys:"
            // envMap.keySet().sort().each { println "    - $it" }
            println ""
        }

        commandLine cliBin.absolutePath, envFileName
    }

    appProject.tasks.named("preBuild").configure {
        dependsOn "generateEnv"
    }
}
